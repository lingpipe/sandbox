model {

  pi ~ dbeta(1,1)
  for (i in 1:I) {
    c[i] ~ dbern(pi)
  }

  mu.beta.0 <- 0
  sigma.beta.0 <- 1
  tau.beta.0 <- pow(sigma.beta.0,-2)

  mu.beta.1 ~ dnorm(0,.0001) I(0,)
  sigma.beta.1 <- 0.5
  tau.beta.1 <- pow(sigma.beta.1,-2)

  for (i in 1:I) {
    mu.beta[i] <- c[i] * mu.beta.1 + (1 - c[i]) * mu.beta.0
    tau.beta[i] <- c[i] * tau.beta.1 + (1 - c[i]) * tau.beta.0
    beta[i] ~ dnorm(mu.beta[i],tau.beta[i])
  }

  mu.alpha ~ dnorm(0,.0001) I(0,)
  sigma.alpha ~ dunif(0.25,10)
  tau.alpha <- pow(sigma.alpha,-2)
  for (j in 1:J) {
    alpha[j] ~ dnorm(mu.alpha,tau.alpha)
    delta[j] ~ dunif(0.1,5)
  }

  for (k in 1:K) {
    logit(bern[k]) <- delta[jj[k]] * (beta[ii[k]] - alpha[jj[k]])
    bern.adj[k] <- max(0,min(1,bern[k]))
    xx[k] ~ dbern(bern.adj[k])
  }
}

